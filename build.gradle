plugins {
	id 'org.springframework.boot' version '2.7.2'
	id 'io.spring.dependency-management' version '1.0.12.RELEASE'
	id 'org.graalvm.buildtools.native' version '0.9.13'
	id 'com.google.cloud.tools.jib' version '3.2.1'
	id 'org.springframework.experimental.aot' version '0.12.1'
	id 'java'
}

ext {
	set('dockerRegistry', "goafabric")
	set('springCloudVersion', "2021.0.3")
	set('springDocOpenApiVersion', "1.6.9")
}

group = 'org.goafabric'
version = '2.0.1-gradle-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	maven { url 'https://repo.spring.io/release' }
	mavenCentral()
}

jib {
	from.image = 'ibm-semeru-runtimes:open-17.0.3_7-jre-focal@sha256:e89df25f95379321ef61d5119eee68c28884ff8f8c5deaf2d474f3cdb1dbbb7e'
	from.platforms = [com.google.cloud.tools.jib.gradle.PlatformParameters.of("linux/amd64"), com.google.cloud.tools.jib.gradle.PlatformParameters.of("linux/arm64")]
	to.image = "${dockerRegistry}/${project.name}"
	to.tags = project.provider{[version]}
	container.jvmFlags = ['-Xms256m', '-Xmx256m']
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencies {
	//web
	implementation 'org.springframework.boot:spring-boot-starter-web'

	//monitoring
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'

	implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
	implementation 'org.springframework.cloud:spring-cloud-sleuth-zipkin'

	implementation "org.springdoc:springdoc-openapi-ui:${springDocOpenApiVersion}"
	implementation "org.springdoc:springdoc-openapi-native:${springDocOpenApiVersion}"

	//crosscuting
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-aop'

	//code generation
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	
	//test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

//TODO: Lombok test build for Adapter
//TODO: toggle AOT Compiler

/*
tasks.named('bootBuildImage') {
	builder = 'paketobuildpacks/builder:tiny'
	buildpacks = ["gcr.io/paketo-buildpacks/java-native-image:7.23.0"]
	bootBuildImage.imageName = "goafabric/${project.name}-native:${project.version}"
	environment = ['BP_NATIVE_IMAGE': 'true', 'BP_JVM_VERSION': '17', 'BP_NATIVE_IMAGE_BUILD_ARGUMENTS': '-J-Xmx6000m']
}
*/

